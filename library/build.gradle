plugins {
    id "com.jfrog.bintray" version "1.7"
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 5
        versionName rootProject.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation "com.android.support:recyclerview-v7:26.1.0"
    implementation "com.android.support:cardview-v7:26.1.0"
}

afterEvaluate {
    bintrayUpload.dependsOn 'assembleRelease'
    publish.dependsOn 'assembleRelease'
}

android.libraryVariants
publishing {
    publications {
        MaterialSearchView(MavenPublication) {
            groupId = rootProject.group
            artifactId = "MaterialSearchView"
            version = rootProject.version
            artifact getReleaseArtifact()
            artifact sourcesJar
            description = rootProject.description
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.group != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                asNode().appendNode('description', description)
                asNode().children().last() + project.pomXml
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = System.getenv('BINTRAY_USER')
                password = System.getenv('SONATYPE_KEY')
            }
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}

static def getReleaseArtifact() {
    return "build/outputs/aar/library-release.aar"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MaterialSearchView']
    pkg {
        repo = 'MaterialSearchView'
        name = 'MaterialSearchView'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Agraphie/MaterialSearchView.git'
        issueTrackerUrl = 'https://github.com/Agraphie/MaterialSearchView/issues'
        labels = ['searchview', 'material-design', 'android']
        description = rootProject.description
        publicDownloadNumbers = true
        version {
            name = rootProject.version
            released = new Date()
            vcsTag = rootProject.version
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv('BINTRAY_GPG')
            }
        }
    }
}
