plugins {
    id "com.jfrog.bintray" version "1.7"
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile "com.android.support:recyclerview-v7:25.3.1"
    compile "com.android.support:cardview-v7:25.3.1"


    testCompile 'junit:junit:4.12'
}

bintrayUpload.dependsOn 'assembleRelease'
publish.dependsOn 'assembleRelease'

android.libraryVariants
publishing {
    publications {
        MaterialSearchView(MavenPublication) {
            // leaving out dependencies (components.java) intentionally
            groupId "de.clemenskeppler"
            artifactId "MaterialSearchView"
            version rootProject.version
            artifact getReleaseArtifact()
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            credentials {
                username = System.getenv('BINTRAY_USER')
                password = System.getenv('BINTRAY_KEY')
            }
            url 'http://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
    }
}

static def getReleaseArtifact() {
    return "build/outputs/aar/library-release.aar"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MaterialSearchView']
    pkg {
        repo = 'MaterialSearchView'
        name = 'MaterialSearchView'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Agraphie/MaterialSearchView.git'
        issueTrackerUrl = 'https://github.com/Agraphie/MaterialSearchView/issues'
        labels = ['searchview', 'material-design', 'android']
        description = 'A lightweight material search view'
        publicDownloadNumbers = true
        version {
            name = rootProject.version
            released = new Date()
            vcsTag = rootProject.version
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv('BINTRAY_GPG')
            }
        }
    }
}
